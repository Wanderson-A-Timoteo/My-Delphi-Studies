unit SimplePed.Controller.PedidoItens;

interface

uses
  SimplePed.Controller.Pedido.Interfaces, SimplePed.Model.Pedido.Interfaces,
  Data.DB, SimplePed.Model.Entidade.PedidoItens, System.Generics.Collections;

Type
  TControllerPedidoItens = class(TInterfacedObject, iControllerPedidoItens)
  private
    FModel: iModelPedidoItens;
    FDataSource: TDataSource;
    FList: TObjectList<TPEDIDOITENS>;
  public
    constructor Create;
    destructor Destroy; override;
    class function New: iControllerPedidoItens;
    function DataSource(aDataSource: TDataSource): iControllerPedidoItens;
    function Buscar: iControllerPedidoItens; overload;
    function Buscar(aId: Integer): iControllerPedidoItens; overload;
    function Insert: iControllerPedidoItens;
    function Delete: iControllerPedidoItens;
    function Update: iControllerPedidoItens;
    function Entidade: TPEDIDOITENS;
  end;

implementation

uses
  System.SysUtils, SimplesPed.Model;

{ TControllerPedidoItens }

function TControllerPedidoItens.Buscar: iControllerPedidoItens;
begin
  Result := Self;

  if Assigned(FList) then
    FList.Free;
  FDataSource.DataSet.DisableControls;
  FList := FModel.DAO.Find;
  FDataSource.DataSet.EnableControls;
end;

function TControllerPedidoItens.Buscar(aId: Integer): iControllerPedidoItens;
begin
  Result := Self;

  if Assigned(FList) then
    FList.Free;

  FDataSource.DataSet.DisableControls;
  FList := FModel.DAO
            .SQL
              .Fields('PEDIDOITENS.ID AS ID,')
              .Fields('PRODUTO.DESCRICAO AS PRODUTO,')
              .Fields('PEDIDOITENS.QUANTIDADE AS QNT,')
              .Fields('PEDIDOITENS.VALORUNITARIO AS UNITARIO,')
              .Fields('PEDIDOITENS.VALORTOTAL AS TOTAL')
              .Join('INNER JOIN PRODUTO ON PRODUTO.ID = PEDIDOITENS.ID_PRODUTO')
              .Where('ID_PEDIDO = ' + IntToStr(aID))
            .&End
            .Find;
  FDataSource.DataSet.EnableControls;
  FDataSource.DataSet.FieldByName('ID').Visible := false;
  FDataSource.DataSet.FieldByName('PRODUTO').DisplayWidth := 100;
end;

constructor TControllerPedidoItens.Create;
begin
  FModel := TModel.New.PedidoItens;
end;

function TControllerPedidoItens.DataSource(aDataSource: TDataSource)
  : iControllerPedidoItens;
begin
  Result := Self;
  FDataSource := aDataSource;
  FModel.DataSource(FDataSource);
end;

function TControllerPedidoItens.Delete: iControllerPedidoItens;
begin
  Result := Self;
  FModel.DAO.Delete(FModel.Entidade);
end;

destructor TControllerPedidoItens.Destroy;
begin

  if Assigned(FList) then
    FList.Free;

  inherited;
end;

function TControllerPedidoItens.Entidade: TPEDIDOITENS;
begin
  Result := FModel.Entidade;
end;

function TControllerPedidoItens.Insert: iControllerPedidoItens;
begin
  Result := Self;
  FModel.DAO.Insert(FModel.Entidade);
end;

class function TControllerPedidoItens.New: iControllerPedidoItens;
begin
  Result := Self.Create;
end;

function TControllerPedidoItens.Update: iControllerPedidoItens;
begin
  Result := Self;
  FModel.DAO.Update(FModel.Entidade);
end;

end.
